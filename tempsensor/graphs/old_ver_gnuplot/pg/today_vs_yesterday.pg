#!/usr/bin/gnuplot
set terminal svg enhanced size 1150,450 font "Arial,9"
set output '/dev/null'
set datafile separator ","

# Retrieve statistical properties
set xdata time
set timefmt "%Y%m%d %H%M%S"
plot '<cat /home/aorith/tempsensor/data/*.dat' u 1:2
min_y = GPVAL_DATA_Y_MIN
max_y = GPVAL_DATA_Y_MAX
min_x = GPVAL_DATA_X_MIN
max_x = GPVAL_DATA_X_MAX
f(x) = mean_y
set fit quiet
set fit logfile '/dev/null'
fit f(x) '<cat /home/aorith/tempsensor/data/*.dat' u 0:2 via mean_y

set output '/var/www/html/temp/00_today_vs_yesterday.svg'

set grid
set tics out nomirror
set border 3 front linetype black linewidth 0.5 dashtype solid
set style line 1 linetype 1 linecolor rgb "red" linewidth 1.0

set xdata time
set timefmt "%H%M%S"
set format x "%H:%S"
set yrange [min_y:max_y]
set xrange ['00:00:00':'23:59:59']
set tics font "Arial, 8"
set ytics 0.5
set y2tics 1
set xtics 3600
#set xtics rotate
#set key inside bottom right
set key inside top left
set key font "Courier new,9"
#set xlabel 't'
#set ylabel 'CÂº'
#set autoscale x
#set autoscale y
set title 'Today vs yesterday'
plot "<grep `date +%Y%m%d` /home/aorith/tempsensor/data/`date +%Y`.dat|cut -d' ' -f2,3|cut -d',' -f1,2" u 1:2 t 'Temp today' w lines lw 3.5 lc rgb "#ff3333", \
     "<grep `date -d 'yesterday' +%Y%m%d` /home/aorith/tempsensor/data/`date -d 'yesterday' +%Y`.dat|cut -d' ' -f2,3|cut -d',' -f1,2" u 1:2 t 'Temp yesterday' w lines lw 1.5 lc rgb "#aa4433", \
'<grep `date +%Y%m%d` /home/aorith/tempsensor/data/*`date +%Y`.dat|cut -d" " -f2,3|cut -d"," -f1,3' u 1:2 w lines lt 1 lc rgb "#6666cc" lw 2.5 t 'Hum today' axes x1y2, \
'<grep `date -d "yesterday" +%Y%m%d` /home/aorith/tempsensor/data/*`date +%Y`.dat|cut -d" " -f2,3|cut -d"," -f1,3' u 1:2 w lines lt 1 lc rgb "#3333aa" lw 1 t 'Hum yesterday' axes x1y2
